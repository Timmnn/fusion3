/*
    --------------------------------------------------
    WHITESPACES AND COMMENTS
    --------------------------------------------------
*/
comment              = _{ "//" ~ (!NEWLINE ~ ANY)* }
WHITESPACE           = _{ (" " | "\t" | "\n" | "\r" | comment)+ }
NEWLINE              = _{ "\n" | "\r\n" }
expression_seperator =  { NEWLINE | ";" }
parameter_seperator  =  { NEWLINE | "," }
/*
    --------------------------------------------------
    PROGRAM STRUCTURE
    --------------------------------------------------
*/
program = { SOI ~ expression ~ (expression_seperator ~ expression)* ~ expression_seperator* ~ EOI }
// A block creates a new scope and may return a value. like a function thats called instantly with every variable as a parameter.
block = { "{" ~ (expression ~ NEWLINE?)* ~ "}" }
// Identifiers
ident = @{
    ((alpha ~ (alpha | digit | "_")*) | (digit ~ alpha ~ (alpha | digit | "_")*) | ("_" ~ alpha ~ (alpha | digit | "_")*))
}
alpha = _{ 'a'..'z' | 'A'..'Z' | "*" | "#" | "~" | "^" | "$" }
digit = _{ '0'..'9' }

expression = { func_def | var_decl | assignment | add_expr }

add_expr = { mul_expr ~ ((add | subtract) ~ mul_expr)* }

mul_expr = { primary ~ ((multiply | divide) ~ primary)* }

primary = {
    int_lit
  | float_lit
  | str_lit
  | func_call
  | struct_init
  | block
  | func_def
  | "(" ~ expression ~ ")"
}

/*
    --------------------------------------------------
    OPERATORS
    --------------------------------------------------
*/
add              = { "+" }
subtract         = { "-" }
multiply         = { "*" }
divide           = { "/" }
and              = { "and" }
or               = { "or" }
equal            = { "==" }
greater_than     = { ">" }
less_than        = { "<" }
unequal          = { "!=" }
greater_or_equal = { ">=" }
less_or_equal    = { "<=" }
/*
    --------------------------------------------------
    VALUE LITERALS
    --------------------------------------------------
*/
int_lit      = @{ "-"? ~ ASCII_DIGIT+ }
str_lit      =  { "\"" ~ inner_string ~ "\"" }
inner_string = @{
    (!("\"" | "\\") ~ ANY | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t") | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4}))*
}
float_lit    = @{ ASCII_DIGIT* ~ "." ~ ASCII_DIGIT+ }
/*
    --------------------------------------------------
    FUNCTIONS
    --------------------------------------------------
*/
func_def       = { "fn" ~ ident ~ "(" ~ param_def_list? ~ ")" ~ return_type? ~ block }
return_type    = { ident }
param_def_list = { field_def ~ ("," ~ field_def)* }
func_call      = { ident ~ "(" ~ param_list? ~ ")" }
param_list     = { expression ~ ("," ~ expression)* }
/*
    --------------------------------------------------
    STRUCTS
    --------------------------------------------------
*/
struct_def         =  { "type" ~ ident ~ "=" ~ "{" ~ struct_def_content? ~ "}" }
struct_def_content =  { field_def ~ ("," ~ field_def)* }
field_ident        = @{ (alpha | digit)+ }
field_def          =  { field_ident ~ ":" ~ ident }
struct_init        =  { ident ~ "{" ~ struct_field_init ~ ("," ~ struct_field_init)* ~ "}" }
struct_field_init  =  { ident ~ ":" ~ expression }
/*
    --------------------------------------------------
    TYPE ALIAS
    --------------------------------------------------
*/
type_alias = { "type" ~ ident ~ "=" ~ ident }
/*
    --------------------------------------------------
    VARIABLE ASSIGNMENTS
    --------------------------------------------------
*/
assignment = _{ ident ~ "=" ~ expression }
var_decl   =  { "let" ~ ident ~ "=" ~ expression }
/*
    --------------------------------------------------
    C IMPORT
    --------------------------------------------------
*/
c_import = { ("_c_import" | "*c*import") ~ str_lit }
